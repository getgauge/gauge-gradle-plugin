import org.gauge.gradle.GaugeTask

plugins {
    id 'org.gauge'
}

repositories {
    mavenLocal()
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

sourceSets {
    spec {
        java.srcDirs('src/spec/')
    }
    test {
        compileClasspath += sourceSets.spec.runtimeClasspath
        runtimeClasspath += sourceSets.spec.runtimeClasspath
    }
}

dependencies {
    testImplementation 'com.thoughtworks.gauge:gauge-java:+'
    testImplementation("org.junit.jupiter:junit-jupiter:6.+")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

tasks.register('cleanBuildDir', Delete) {
    delete "${projectDir}/logs"
    delete "${projectDir}/out"
    delete "${projectDir}/reports"
}
tasks.clean.dependsOn("cleanBuildDir")

gauge {
    specsDir = 'specs'
    inParallel = true
    nodes = 4
    additionalFlags = '--verbose'
}

def gaugeDev = tasks.register('gaugeDev', GaugeTask) {
    doFirst {
        gauge {
            specsDir = 'specs'
            inParallel = true
            nodes = 2
            env = 'dev'
            additionalFlags = '--verbose'
        }
    }
}

def gaugeDevRepeat = tasks.register('gaugeDevRepeat', GaugeTask) {
    dependsOn(gaugeDev)
    doFirst {
        gauge {
            additionalFlags = '--repeat --simple-console'
        }
    }
}

tasks.withType(AbstractTestTask).configureEach {
    failOnNoDiscoveredTests = false
}

def pluginCheck = gradle.includedBuild("plugin").task(":check")
tasks.gaugeValidate.dependsOn(pluginCheck)
tasks.gaugeDev.dependsOn(pluginCheck)

tasks.check.dependsOn(gaugeValidate, gaugeDevRepeat)
