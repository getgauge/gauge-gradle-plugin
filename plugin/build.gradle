plugins {
    id 'java-gradle-plugin'
    id 'jvm-test-suite'
    id 'com.gradle.plugin-publish' version '2.0.0'
    id 'pmd'
}

repositories {
    mavenLocal()
    mavenCentral()
}

group = 'org.gauge.gradle'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

pmd {
    consoleOutput = true
}

testing {
    suites {
        configureEach {
            useJUnitJupiter()
            dependencies {
                implementation "org.junit.jupiter:junit-jupiter:6.+"
                runtimeOnly "org.junit.platform:junit-platform-launcher"
            }
        }
        integrationTest(JvmTestSuite) {
            dependencies {
                implementation project()
                implementation 'commons-io:commons-io:2.21.+'
                implementation 'org.hamcrest:hamcrest:3.0'
            }
            targets {
                all {
                    testTask.configure {
                        javaLauncher = javaToolchains.launcherFor {
                            languageVersion = JavaLanguageVersion.of(17)
                        }
                        shouldRunAfter(test)
                    }
                }
            }
        }
    }
}

tasks.withType(Test).configureEach {
    testLogging {
        showStandardStreams = true
        showStackTraces = true
        exceptionFormat = 'full'
        events = ["passed", "failed", "skipped"]
    }
}

tasks.check.dependsOn(tasks.named("integrationTest"))

gradlePlugin {
    testSourceSets(sourceSets.integrationTest)
    plugins {
        vcsUrl = "https://github.com/getgauge/gauge-gradle-plugin.git"
        website = "https://github.com/getgauge/gauge-gradle-plugin"
        gauge {
            id = "org.gauge"
            implementationClass = "org.gauge.gradle.GaugePlugin"
            displayName = "Gauge"
            description = "Gradle plugin for Gauge, the open source test automation tool developed by ThoughtWorks."
            tags.addAll('gauge', 'thoughtworks', 'testing')
        }
    }
}